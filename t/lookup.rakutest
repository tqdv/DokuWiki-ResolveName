use Test;
use DokuWiki::PageName;
use DokuWiki::PageLink;
use DokuWiki::ResolveName;

plan 10 + (1 + 3 + 1 + 1 + 1) + 2;

my @lookups =
	('sample:page', 'follow',        ':sample:follow',     "Simple link"),
	('sample:page', '.same:space',   ':sample:same:space', "Same namespace link"),
	('sample:page', '..previous',    ':previous',          "Previous namespace"),
	('sample:page', 'absolute:link', ':absolute:link',     "Absolute link"),
	('sample:page', '.:a::b',        ':sample:a:b',        "Collapse colons"),
	('sample:page', '..:..:ignored:..::index', ':index',   "Links do not overflow"),
	('playground:playground', '.',   ':playground',        "Single dot links to a page and not the namespace start"),
	('playground:playground', '::',  ':start',             "Only-colon link goes to root start"),
	('sample:page', '',              ':sample:page',       "Empty link links to current page"),
	('sample:page', '---',           ':sample',            "Link of invalid characters is treated as `.`" );

for @lookups -> ($start, $link, $expect, $desc) {
	is resolve-name($start, $link), $expect, $desc;
}

# Test each option

is resolve-name('sample:page', '.:', startpage => 'index'), ':sample:index', "Setting the start optional key works";

is resolve-name('sample:page', '.:', pages => (':sample:sample',)), ':sample:sample', "Namespace links look for ns:ns pages";
is resolve-name('sample:page', '.:', pages => (':sample',)),        ':sample',        "Namespace links look for ns pages";
is resolve-name('sample:page', '.:', pages => ('sample:.',)),       ':sample',        "using link style pages still works";

is resolve-name('start', 'name', pages => ('names'), :autoplural), ':names', "Autoplural works";

is resolve-name('some/page/link', '../no', :useslash), ':some:no', "Useslash works";

is resolve-name('some$$$page:thing', '.:weird%%:link:--', :sepchar('.')), ':some.page:weird:link', "Sepchar works";

# Multiple options at once
is resolve-name('sample:page', ':', pages => ('indexs'), startpage => 'index', :autoplural), ':index', "Autoplural doesn't interfere with startpage";
is resolve-name('sample:page', '.:', pages => ('sample'), startpage => 'index'), ':sample', "Using both startpage and pages options, works";
