use Test;
use DokuWiki::PageName;

plan 1 + 4 + 6;
{
	my DokuWiki::PageName $page .= new('some:page');
	my $new = $page.resolve-name('');
	$new.namespaces.unshift('buy');

	#diag $page ~ " vs. " ~ $new;
	ok $page.namespaces !eqv $new.namespaces, "Resolving a link creates a new independant object";
}

diag "Link roundtrips";

my @links =
	('simple:link',          'simple:link',          "Link roundtrip"),
	(':colons:and:cologne:', ':colons:and:cologne:', "Leading and trailing colons in link"),
	('.leading:dot',         '.:leading:dot',        "Leading single dot is normalized"),
	('..:double:dot',        '..:double:dot',        "Leading double dot is normalized");

for @links -> ($code, $expect, $desc) {
	is DokuWiki::PageLink.new($code), $expect, $desc;
}

diag "PageName roundtrips";

my @pages =
	(':absolute:page:path', ':absolute:page:path', "Absolute page roundtrip"),
	('relative:page:path',  ':relative:page:path', "Relative page roundtrip"),
	('some:space:',         ':some:space:start',   "Missing page name"),
	('collapse::colons',    ':collapse:colons',    "Collapsing colons"),
	('...more:dots',        ':more:dots',          "More than 3 dots are ignored"),
	('invalid:---:chars',   ':invalid:chars',      "Invalid characters are removed before collapsing");

for @pages -> ($code, $expect, $desc) {
	is DokuWiki::PageName.new($code), $expect, $desc;
}