use Test;
use DokuWiki::PageName;
use DokuWiki::PageName::App;

plan 1 + 11 + 1;
{
	my DokuWiki::PageName $page .= new('some:page');
	my $new = $page.resolve-name('');
	$new.namespaces.unshift('buy');

	#diag $page ~ " vs. " ~ $new;
	ok $page.namespaces !eqv $new.namespaces, "Resolving a link creates a new independant object";
}

diag "PageName roundtrips";

my @pages =
	(':absolute:page:path', ':absolute:page:path', "Absolute page roundtrip"),
	('relative:page:path',  ':relative:page:path', "Relative page roundtrip"),
	('some:space:',         ':some:space:start',   "Missing page name"),
	('collapse::colons',    ':collapse:colons',    "Collapsing colons"),
	('...more:dots',        ':more:dots',          "More than 3 dots are ignored"),
	('invalid:---:chars',   ':invalid:chars',      "Invalid characters are removed before collapsing"),
	('sample;page',         ':sample:page',        "Semi-colons are equivalent to colons"),
	('sample:page$@%name',  ':sample:page_name',   "Multiple invalid characters are replaced with a single sepchar"),
	('sample:..:page',      ':sample:page',        "Double dots are ignored"),
	('playground:!!!',      ':playground',         "Invalid characters are removed"),
	('pg:!!!:',             ':pg:start',           "Invalid characters still recognized as the cleaned link namespaced");

for @pages -> ($code, $expect, $desc) {
	is DokuWiki::PageName.new($code), $expect, $desc;
}

{
	my $app =  DokuWiki::PageName::App.new(pages => ('playground:playground',));
	is DokuWiki::PageName.new('playground:', :$app), ':playground:playground', "Namespace page resolution works";
}
